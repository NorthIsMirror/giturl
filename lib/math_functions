setopt typesetsilent

local -a characters
characters=( 0 1 2 3 4 5 6 7 8 9 a b c d e f g h i j k l m n o p q r s t u v w x y z A B C D E F G H I J K L M N O P Q R S T U V W X Y Z

# Latin-1 Supplement [µºÀ-ÖØ-áãäæ-öøú-ÿ]
µ º À Á Â Ã Ä Å Æ Ç È É Ê Ë Ì Í Î Ï Ð Ñ Ò Ó Ô Õ Ö Ø Ù Ú Û Ü Ý Þ ß à á ã ä æ ç è é ê ë ì í î ï ð ñ ò ó ô õ ö ø ú û ü ý þ ÿ

# Latin Extended-A [ÿ-ſ]
Ā ā Ă ă Ą ą Ć ć Ĉ ĉ Ċ ċ Č č Ď ď Đ đ Ē ē Ĕ ĕ Ė ė Ę ę Ě ě Ĝ ĝ Ğ ğ Ġ ġ Ģ ģ Ĥ ĥ Ħ ħ Ĩ ĩ Ī ī Ĭ ĭ Į į İ ı Ĳ ĳ Ĵ ĵ Ķ ķ ĸ Ĺ ĺ Ļ ļ Ľ ľ Ŀ ŀ Ł ł Ń ń
Ņ ņ Ň ň ŉ Ŋ ŋ Ō ō Ŏ ŏ Ő ő Œ œ Ŕ ŕ Ŗ ŗ Ř ř Ś ś Ŝ ŝ Ş ş Š š Ţ ţ Ť ť Ŧ ŧ Ũ ũ Ū ū Ŭ ŭ Ů ů Ű ű Ų ų Ŵ ŵ Ŷ ŷ Ÿ Ź ź Ż ż Ž ž ſ

# Latin Extended-B [ſ-ɏ]
ƀ Ɓ Ƃ ƃ Ƅ ƅ Ɔ Ƈ ƈ Ɖ Ɗ Ƌ ƌ ƍ Ǝ Ə Ɛ Ƒ ƒ Ɠ Ɣ ƕ Ɩ Ɨ Ƙ ƙ ƚ ƛ Ɯ Ɲ ƞ Ɵ Ơ ơ Ƣ ƣ Ƥ ƥ Ʀ Ƨ ƨ Ʃ ƪ ƫ Ƭ ƭ Ʈ Ư ư Ʊ Ʋ Ƴ ƴ Ƶ ƶ Ʒ Ƹ ƹ ƺ ƻ Ƽ ƽ ƾ ƿ ǀ ǁ ǂ ǃ Ǆ
ǅ ǆ Ǉ ǈ ǉ Ǌ ǋ ǌ Ǎ ǎ Ǐ ǐ Ǒ ǒ Ǔ ǔ Ǖ ǖ Ǘ ǘ Ǚ ǚ Ǜ ǜ ǝ Ǟ ǟ Ǡ ǡ Ǣ ǣ Ǥ ǥ Ǧ ǧ Ǩ ǩ Ǫ ǫ Ǭ ǭ Ǯ ǯ ǰ Ǳ ǲ ǳ Ǵ ǵ Ƕ Ƿ Ǹ ǹ Ǻ ǻ Ǽ ǽ Ǿ ǿ Ȁ ȁ Ȃ ȃ Ȅ ȅ Ȇ ȇ Ȉ ȉ
Ȋ ȋ Ȍ ȍ Ȏ ȏ Ȑ ȑ Ȓ ȓ Ȕ ȕ Ȗ ȗ Ș ș Ț ț Ȝ ȝ Ȟ ȟ Ƞ ȡ Ȣ ȣ Ȥ ȥ Ȧ ȧ Ȩ ȩ Ȫ ȫ Ȭ ȭ Ȯ ȯ Ȱ ȱ Ȳ ȳ ȴ ȵ ȶ ȷ ȸ ȹ Ⱥ Ȼ ȼ Ƚ Ⱦ ȿ ɀ Ɂ ɂ Ƀ Ʉ Ʌ Ɇ ɇ Ɉ ɉ Ɋ ɋ Ɍ ɍ Ɏ ɏ

# Latin Extended Additional [Ḁ-Ḵ]
Ḁ ḁ Ḃ ḃ Ḅ ḅ Ḇ ḇ Ḉ ḉ Ḋ ḋ Ḍ ḍ Ḏ ḏ Ḑ ḑ Ḓ ḓ Ḕ ḕ Ḗ ḗ Ḙ ḙ Ḛ ḛ Ḝ ḝ Ḟ ḟ Ḡ ḡ Ḣ ḣ Ḥ ḥ Ḧ ḧ Ḩ ḩ Ḫ ḫ Ḭ ḭ Ḯ ḯ Ḱ ḱ Ḳ ḳ Ḵ

# Character class for all: [0-9a-zA-ZµºÀ-ÖØ-áãäæ-öøú-ɏḀ-Ḵ]
)

local -A characters_map
characters_map=(
0 0 1 1 2 2 3 3 4 4 5 5 6 6 7 7 8 8 9 9 a 10 b 11 c 12 d 13 e 14 f 15 g 16 h 17 i 18 j 19
k 20 l 21 m 22 n 23 o 24 p 25 q 26 r 27 s 28 t 29 u 30 v 31 w 32 x 33 y 34 z 35 A 36 B 37 C 38 D 39
E 40 F 41 G 42 H 43 I 44 J 45 K 46 L 47 M 48 N 49 O 50 P 51 Q 52 R 53 S 54 T 55 U 56 V 57 W 58 X 59
Y 60 Z 61 µ 62 º 63 À 64 Á 65 Â 66 Ã 67 Ä 68 Å 69 Æ 70 Ç 71 È 72 É 73 Ê 74 Ë 75 Ì 76 Í 77 Î 78 Ï 79
Ð 80 Ñ 81 Ò 82 Ó 83 Ô 84 Õ 85 Ö 86 Ø 87 Ù 88 Ú 89 Û 90 Ü 91 Ý 92 Þ 93 ß 94 à 95 á 96 ã 97 ä 98 æ 99
ç 100 è 101 é 102 ê 103 ë 104 ì 105 í 106 î 107 ï 108 ð 109 ñ 110 ò 111 ó 112 ô 113 õ 114 ö 115 ø 116 ú 117 û 118 ü 119
ý 120 þ 121 ÿ 122 Ā 123 ā 124 Ă 125 ă 126 Ą 127 ą 128 Ć 129 ć 130 Ĉ 131 ĉ 132 Ċ 133 ċ 134 Č 135 č 136 Ď 137 ď 138 Đ 139
đ 140 Ē 141 ē 142 Ĕ 143 ĕ 144 Ė 145 ė 146 Ę 147 ę 148 Ě 149 ě 150 Ĝ 151 ĝ 152 Ğ 153 ğ 154 Ġ 155 ġ 156 Ģ 157 ģ 158 Ĥ 159
ĥ 160 Ħ 161 ħ 162 Ĩ 163 ĩ 164 Ī 165 ī 166 Ĭ 167 ĭ 168 Į 169 į 170 İ 171 ı 172 Ĳ 173 ĳ 174 Ĵ 175 ĵ 176 Ķ 177 ķ 178 ĸ 179
Ĺ 180 ĺ 181 Ļ 182 ļ 183 Ľ 184 ľ 185 Ŀ 186 ŀ 187 Ł 188 ł 189 Ń 190 ń 191 Ņ 192 ņ 193 Ň 194 ň 195 ŉ 196 Ŋ 197 ŋ 198 Ō 199
ō 200 Ŏ 201 ŏ 202 Ő 203 ő 204 Œ 205 œ 206 Ŕ 207 ŕ 208 Ŗ 209 ŗ 210 Ř 211 ř 212 Ś 213 ś 214 Ŝ 215 ŝ 216 Ş 217 ş 218 Š 219
š 220 Ţ 221 ţ 222 Ť 223 ť 224 Ŧ 225 ŧ 226 Ũ 227 ũ 228 Ū 229 ū 230 Ŭ 231 ŭ 232 Ů 233 ů 234 Ű 235 ű 236 Ų 237 ų 238 Ŵ 239
ŵ 240 Ŷ 241 ŷ 242 Ÿ 243 Ź 244 ź 245 Ż 246 ż 247 Ž 248 ž 249 ſ 250 ƀ 251 Ɓ 252 Ƃ 253 ƃ 254 Ƅ 255 ƅ 256 Ɔ 257 Ƈ 258 ƈ 259
Ɖ 260 Ɗ 261 Ƌ 262 ƌ 263 ƍ 264 Ǝ 265 Ə 266 Ɛ 267 Ƒ 268 ƒ 269 Ɠ 270 Ɣ 271 ƕ 272 Ɩ 273 Ɨ 274 Ƙ 275 ƙ 276 ƚ 277 ƛ 278 Ɯ 279
Ɲ 280 ƞ 281 Ɵ 282 Ơ 283 ơ 284 Ƣ 285 ƣ 286 Ƥ 287 ƥ 288 Ʀ 289 Ƨ 290 ƨ 291 Ʃ 292 ƪ 293 ƫ 294 Ƭ 295 ƭ 296 Ʈ 297 Ư 298 ư 299
Ʊ 300 Ʋ 301 Ƴ 302 ƴ 303 Ƶ 304 ƶ 305 Ʒ 306 Ƹ 307 ƹ 308 ƺ 309 ƻ 310 Ƽ 311 ƽ 312 ƾ 313 ƿ 314 ǀ 315 ǁ 316 ǂ 317 ǃ 318 Ǆ 319
ǅ 320 ǆ 321 Ǉ 322 ǈ 323 ǉ 324 Ǌ 325 ǋ 326 ǌ 327 Ǎ 328 ǎ 329 Ǐ 330 ǐ 331 Ǒ 332 ǒ 333 Ǔ 334 ǔ 335 Ǖ 336 ǖ 337 Ǘ 338 ǘ 339
Ǚ 340 ǚ 341 Ǜ 342 ǜ 343 ǝ 344 Ǟ 345 ǟ 346 Ǡ 347 ǡ 348 Ǣ 349 ǣ 350 Ǥ 351 ǥ 352 Ǧ 353 ǧ 354 Ǩ 355 ǩ 356 Ǫ 357 ǫ 358 Ǭ 359
ǭ 360 Ǯ 361 ǯ 362 ǰ 363 Ǳ 364 ǲ 365 ǳ 366 Ǵ 367 ǵ 368 Ƕ 369 Ƿ 370 Ǹ 371 ǹ 372 Ǻ 373 ǻ 374 Ǽ 375 ǽ 376 Ǿ 377 ǿ 378 Ȁ 379
ȁ 380 Ȃ 381 ȃ 382 Ȅ 383 ȅ 384 Ȇ 385 ȇ 386 Ȉ 387 ȉ 388 Ȋ 389 ȋ 390 Ȍ 391 ȍ 392 Ȏ 393 ȏ 394 Ȑ 395 ȑ 396 Ȓ 397 ȓ 398 Ȕ 399
ȕ 400 Ȗ 401 ȗ 402 Ș 403 ș 404 Ț 405 ț 406 Ȝ 407 ȝ 408 Ȟ 409 ȟ 410 Ƞ 411 ȡ 412 Ȣ 413 ȣ 414 Ȥ 415 ȥ 416 Ȧ 417 ȧ 418 Ȩ 419
ȩ 420 Ȫ 421 ȫ 422 Ȭ 423 ȭ 424 Ȯ 425 ȯ 426 Ȱ 427 ȱ 428 Ȳ 429 ȳ 430 ȴ 431 ȵ 432 ȶ 433 ȷ 434 ȸ 435 ȹ 436 Ⱥ 437 Ȼ 438 ȼ 439
Ƚ 440 Ⱦ 441 ȿ 442 ɀ 443 Ɂ 444 ɂ 445 Ƀ 446 Ʉ 447 Ʌ 448 Ɇ 449 ɇ 450 Ɉ 451 ɉ 452 Ɋ 453 ɋ 454 Ɍ 455 ɍ 456 Ɏ 457 ɏ 458 Ḁ 459
ḁ 460 Ḃ 461 ḃ 462 Ḅ 463 ḅ 464 Ḇ 465 ḇ 466 Ḉ 467 ḉ 468 Ḋ 469 ḋ 470 Ḍ 471 ḍ 472 Ḏ 473 ḏ 474 Ḑ 475 ḑ 476 Ḓ 477 ḓ 478 Ḕ 479
ḕ 480 Ḗ 481 ḗ 482 Ḙ 483 ḙ 484 Ḛ 485 ḛ 486 Ḝ 487 ḝ 488 Ḟ 489 ḟ 490 Ḡ 491 ḡ 492 Ḣ 493 ḣ 494 Ḥ 495 ḥ 496 Ḧ 497 ḧ 498 Ḩ 499
ḩ 500 Ḫ 501 ḫ 502 Ḭ 503 ḭ 504 Ḯ 505 ḯ 506 Ḱ 507 ḱ 508 Ḳ 509 ḳ 510 Ḵ 511
)

# FUNCTION: letters_to_numbers {{{
# Converts each character of input string into number 0..61
#
letters_to_numbers() {
    local letters="$1" l

    reply=( )

    if [[ "$letters" != [0-9a-zA-ZµºÀ-ÖØ-áãäæ-öøú-ɏḀ-Ḵ]## ]]; then
        print "Incorrect character detected in: '$letters'. Gcode should consist only from a-z, A-Z, 0-9 characters, plus 450 unicode characters [µºÀ-ÖØ-áãäæ-öøú-ɏḀ-Ḵ]"
        return 1
    fi

    integer number
    for l in "${(@s::)letters}"; do
        number="${characters_map[$l]}"
        reply+=( "$number" )
    done

    return 0
}
# }}}
# FUNCTION: numbers_to_letters {{{
# Converts array of numbers into string [0-9a-zA-Z<unicode chars>]+
numbers_to_letters() {
    REPLY=""
    integer i
    for i in "$@"; do
        if [[ "$i" -lt 0 || "$i" -gt 511 ]]; then
            print "Incorrect number during character conversion: $i"
            return 1
        fi
        REPLY+="${characters[i+1]}"
    done

    return 0
}
# }}}
# FUNCTION: decode_gcode {{{
# Turns gcode into bits
decode_gcode() {
    local letters="$1"

    local -a bits
    local workingvar="$letters"
    while [ "${workingvar//0/}" != "" ]; do
        div2 "$workingvar"
        workingvar="${reply[1]}"
        bits=( "${reply[2]}" "${bits[@]}" )
        # print "After div $workingvar/${reply[2]}"
    done
    # print "Bits of the letters $letters are: ${(j::)bits[@]}"
    reply=( "${bits[@]}" )
}
# }}}
# FUNCTION: get_integer_from_base_512 {{{
# Converts given base-512 string into integer
# Warning: it tagets integer (signed), so
# the size of number is limited here (while
# decode_gcode generates series of bits of
# arbitrary length)
get_integer_from_base_512() {
    local letters="$1"
    local -a bits
    local workingvar="$letters"

    # Get base-2 generated array consisting of 1 and 0
    while [ "${workingvar//0/}" != "" ]; do
        div2 "$workingvar"
        workingvar="${reply[1]}"
        bits=( "${reply[2]}" "${bits[@]}" )
    done

    # Now sum up the obtained 0 and 1
    integer i mul=1 size="${#bits}"
    REPLY="0"
    for (( i=size; i>=1; i-- )); do
        REPLY=$(( REPLY + bits[i]*mul ))
        mul=$(( mul * 2 ))
    done

    # TODO: detect overflow and other problems
    return 0
}
# }}}
# FUNCTION: encode_gcode_str01 {{{
# Takes string of 0 and 1 and encodes it to base-512
# number expressed via 0-9a-zA-Z<Unicode chars>
#
encode_gcode_str01() {
    str_01_to_24-bit_pack_numbers "$1"
    encode_gcode_24-bit_pack_numbers "${reply[@]}"

}
# }}}
# FUNCTION: encode_gcode_arr01 {{{
# Takes array (positional parameters) of 0 and 1 and encodes
# it to base-512 number expressed via 0-9a-zA-Z<Unicode chars>
#
encode_gcode_arr01() {
    arr_01_to_24-bit_pack_numbers "$@"
    encode_gcode_24-bit_pack_numbers "${reply[@]}"
}
# }}}
# FUNCTION: encode_gcode_24-bit_pack_numbers {{{
# Takes 24-bit pack numbers and encodes them to base-512
# number expressed via 0-9a-zA-Z<Unicode chars>
#
encode_gcode_24-bit_pack_numbers() {
    numbers=( "$@" )

    local -a nums_base512 workingvar
    workingvar=( "${numbers[@]}" )

    integer all_zero=1 i
    for i in "${workingvar[@]}"; do
        if [[ "$i" -ne 0 ]]; then
            all_zero=0
            break
        fi
    done

    while (( all_zero != 1 )); do
        div_24-bit_pack_numbers_512 "${workingvar[@]}"
        workingvar=( "${reply[@]}" )
        nums_base512=( "$REPLY" "${nums_base512[@]}" )

        # Check if workingvar is all zero
        all_zero=1
        for i in "${workingvar[@]}"; do
            if [[ "$i" -ne 0 ]]; then
                all_zero=0
                break
            fi
        done
    done

    # print "Base512 numbers of $str01 are: ${(j:,:)nums_base512[@]}"
    numbers_to_letters "${nums_base512[@]}" # Result in $REPLY
    reply=( "${nums_base512[@]}" )          # Additional result
}
# }}}
# FUNCTION: div2 {{{
# $1 - gcode's letters
# $reply - ( "gcode's letters after division" "remainder 0 or 1" )
div2() {

    #
    # First translate the letters to numbers and put them into array
    #

    local letters="$1"
    local -a numbers
    letters_to_numbers "$letters" || return 1
    numbers=( "${reply[@]}" )

    #
    # Now operate on the array performing long-division
    #

    local -a result
    result=()

    integer prepared_for_division="${numbers[1]}" input
    for input in ${(@)numbers[2,-1]} 0; do
        integer quotient=prepared_for_division/2

        result+=( "$quotient" )

        integer recovered=$(( quotient*2 ))
        integer subtracted=prepared_for_division-recovered

        prepared_for_division=512*subtracted+input
    done

    #
    # Now convert the result to letters
    #

    numbers_to_letters "${result[@]}" || return 2

    #
    # Return
    #

    reply=( "$REPLY" "$subtracted" )

    return 0
}
# }}}
# FUNCTION: str_01_to_24-bit_pack_numbers {{{
# Takes STRING of 0 and 1 and converts it to array of numbers
# that are 24-bit packs taken from right to left, from the string
#
str_01_to_24-bit_pack_numbers() {
    local -a bits
    bits=( "${(@s::)1}" )
    arr_01_to_24-bit_pack_numbers "${bits[@]}"

    return 0
}
# }}}
# FUNCTION: arr_01_to_24-bit_pack_numbers {{{
# Takes ARRAY (@) of 0 and 1 and converts it to array of numbers
# that are 24-bit packs taken from right to left, from the string
#
arr_01_to_24-bit_pack_numbers() {
    local -a bits pack numbers
    bits=( "$@" )
    integer count=0 i size="${#bits}"

    # Take packs of 8 bits, convert each to number and store in array
    for (( i=size; i>=1; i-- )); do
        pack=( "$bits[i]" "${pack[@]}" )
        count+=1
        (( count < 24 && i != 1 )) && continue
        count=0

        # Convert the max. 8 bit pack to number
        integer result=0 p
        for p in "${pack[@]}"; do
            result=result*2+p
        done

        numbers=( "$result" "${numbers[@]}" )
        pack=( )
    done

    reply=( "${numbers[@]}" )
    return 0
}
# }}}
# FUNCTION: div_24-bit_pack_numbers_512 {{{
# $1 - series of 0 and 1
# $reply - ( "series of 0 and 1, result of division" "remainder 0 ... 61" )
div_24-bit_pack_numbers_512() {
    local -a numbers
    numbers=( "$@" )

    # print "Dividing numbers: " "${numbers[@]}"

    #
    # Now operate on the array performing long-division
    #

    integer cur last
    cur=1
    last="${#numbers}"

    local -a result
    result=()

    integer prepared_for_division="${numbers[cur]}"
    while (( 1 )); do
        integer quotient=prepared_for_division/512

        result+=( "$quotient" )

        integer recovered=$(( quotient*512 ))
        integer subtracted=prepared_for_division-recovered

        cur+=1
        if (( cur > last )); then
            break
        fi

        prepared_for_division=16777216*subtracted+${numbers[cur]}
    done

    # print "Result of division: " "${result[@]}"
    # print "Remainder: $subtracted"

    #
    # Return
    #

    reply=( "${result[@]}" )
    REPLY="$subtracted"

    return 0
}
# }}}
# FUNCTION: convert_integer_to_base_512 {{{
# Takes number in $1, returns [0-9a-zA-Z<unicode chars>]+
# - representation of the number in base-512
#
convert_integer_to_base_512() {
    integer number="$1"
    local -a digits

    integer new_number=number
    integer remainder
    while (( new_number != 0 )); do
        remainder=new_number%512
        new_number=new_number/512

        digits+=( "$remainder" )
    done

    [[ "${#digits}" -eq 0 ]] && digits+=( 0 )
    [[ "${#digits}" -eq 1 ]] && digits+=( 0 )
    [[ "${#digits}" -eq 2 ]] && digits+=( 0 )

    digits=( "${(Oa)digits[@]}" )

    numbers_to_letters "${digits[@]}"
}
# }}}

# vim:ft=zsh
